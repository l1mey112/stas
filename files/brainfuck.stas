#include "std.stas"
#use_dump

; echo '+[----->+++<]>+.+.' | ./brainfuck

main ! in do
	local * input_bf [1024]
	input_bf 1024 read_stdin
	
	local * buffer_bf [20000]
    buffer_bf 0 20000 memset ; juust incase

    local int ptr_bf 0
    local int current_ch 0

    local * last_loop 0
    local * current_bf 0

    input_bf pop current_bf
    while current_bf * :: 8 @ 0 != do
        pop current_ch
        
        if current_ch 62 = do      ; >
            ptr_bf ++ pop ptr_bf
        else if current_ch 60 = do ; <
            ptr_bf -- pop ptr_bf
        else if current_ch 43 = do ; +
            buffer_bf ptr_bf += @
            * :: 8 ++ & :: 8
        else if current_ch 45 = do ; -
            buffer_bf ptr_bf += @
            * :: 8 -- & :: 8
        else if current_ch 46 = do ; .
            buffer_bf ptr_bf +=
            1 write
        else if current_ch 44 = do ; ,
            buffer_bf ptr_bf +=
            getch & :: 8
        else if current_ch 91 = do ; [
            current_bf pop last_loop
        else if current_ch 93 = do ; ]
            last_loop 0 != assert
            if buffer_bf ptr_bf += * :: 8 0 != do
                last_loop pop current_bf
            end
        end end end end end end end end ; yikes...
        current_bf ++ pop current_bf
    end
    endl
end