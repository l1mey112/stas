import "modules/rand.stas"
import "modules/stdio.stas"

define ROWS 50 enddef
define sizeof(scanline) 100 enddef

fn main do
	reserve sizeof(scanline) scanline

	scanline 1 write8
	scanline 1 + 20 write8

	6616 srand
	0 while dup sizeof(scanline) < do
		dup 
		
		scanline +
		rand 2 %
		write8

		++
	endwhile
	drop

	0 while dup ROWS < do
		0 while dup sizeof(scanline) < do
			dup
			scanline + deref8
			if
				"*" print
			else
				" " print
			endif

			++
		endwhile
		drop

		endl

		reserve 8 pattern

		scanline deref8 1 <<
		scanline 1 + deref8 |
		pattern swap write64

		1 while dup sizeof(scanline) 1 - < do
			dup dup

			pattern deref64 1 << 7 &
			swap 1 scanline + + deref8 | ; dup 1
			pattern swap write64

			scanline +                   ; dup 2
			110 pattern deref64 >> 1 &
			write8

			++
		endwhile
		drop

		++
	endwhile
	drop
	endl
endfn