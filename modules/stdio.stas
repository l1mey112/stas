include "lib/syscalls.stas"

fn syscall2 3 1 do
	asm 3 1
"	pop rsi
	pop rdi
	pop rax
	syscall
	push rax"
endfn

fn syscall3 4 1 do
	asm 4 1
"	pop rdx
	pop rsi
	pop rdi
	pop rax
	syscall
	push rax"
endfn

fn strlen 1 1 do
	dup
	while dup deref8 0 != do
		++
	endwhile
	swap -
endfn

fn write 2 0 do
	sys_write rotate stdout rotate syscall3
	drop
endfn

fn endl 0 0 do
	reserve 1 newline
	newline 10 write8
	newline 1 write
endfn

fn print 1 0 do
	dup strlen write
endfn

fn println 1 0 do
	print
	endl
endfn

fn uput 1 0 do
	reserve 20 _buf
	reserve Var _pos
	reserve Var _len

	dup

	0 = if
		_buf 48 write8
		_buf 1 write
		ret           ; ret doesn't worry about unhandled
	endif             ; values on the stack

	_len 0 write64
	_pos _buf 20 + write64

	while dup 0 > do
		_pos ptr--

		10 %%
		48 +

		_pos deref64 swap write8

		_len ptr++
	endwhile
	drop

	_pos deref64 _len deref64 write
endfn

fn uputln 1 0 do
	uput
	endl
endfn

fn getch 0 1 do
	reserve 1 ch
	
	0 0 ch 1 syscall3
	drop

	ch deref8 
endfn