const alloc_buffer.default_size { 1024 256 * } ; 256 KiB
auto  alloc_buffer.cap  1
auto  alloc_buffer.len  1
auto  alloc_buffer.data 1

; (size)
fn salloc.init 1 0 {
	alloc_buffer.data 0 != if {
		alloc_buffer.data alloc_buffer.cap munmap 0 = assert -> 'salloc.init: munmap failed'
	}
	dup memory.mmap_anon pop alloc_buffer.data
	                     pop alloc_buffer.cap
}

fn salloc.guard {
	alloc_buffer.data 0 = if {
		alloc_buffer.default_size salloc.init
	}
}

; (size)
fn salloc.guard_size 1 0 {
	salloc.guard
	dup alloc_buffer.len + alloc_buffer.cap >= if {
		alloc_buffer.cap 2 * salloc.init
		salloc.guard_size ; tail recursive!
	} else {
		drop
	}
}


- noinline
; ( -- ptr )
fn salloc.now 0 1 {
	salloc.guard
	alloc_buffer.data alloc_buffer.len +
}

- noinline
; (ptr)
fn salloc.rewind 1 0 {
	salloc.guard
	alloc_buffer.data -
	pop alloc_buffer.len
}

; (str len)
fn salloc.push 2 0 {
	dup salloc.guard_size
	dup rot
	; (len str len)
	alloc_buffer.data alloc_buffer.len +
	rot memcpy
	alloc_buffer.len + pop alloc_buffer.len
}

; (u8)
fn salloc.push_8 1 0 {
	sizeof(u8) salloc.guard_size
	alloc_buffer.data alloc_buffer.len + swap w8
	alloc_buffer.len ++ pop alloc_buffer.len
}

; (u16)
fn salloc.push_16 1 0 {
	sizeof(u16) salloc.guard_size
	alloc_buffer.data alloc_buffer.len + swap w16
	alloc_buffer.len sizeof(u16) + pop alloc_buffer.len
}

; (u32)
fn salloc.push_32 1 0 {
	sizeof(u32) salloc.guard_size
	alloc_buffer.data alloc_buffer.len + swap w32
	alloc_buffer.len sizeof(u32) + pop alloc_buffer.len
}

; (u64)
fn salloc.push_64 1 0 {
	sizeof(u64) salloc.guard_size
	alloc_buffer.data alloc_buffer.len + swap w64
	alloc_buffer.len sizeof(u64) + pop alloc_buffer.len
}

; (val len)
fn salloc.push_memset 2 0 {
	dup salloc.guard_size
	dup rot
	; (len val len)
	alloc_buffer.data alloc_buffer.len + rot memset
	; (len data val len)
	alloc_buffer.len + pop alloc_buffer.len
}

; ( ptr -- str len )
fn salloc.make_str 1 2 {
	dup alloc_buffer.data -
	    alloc_buffer.len swap -
	0 salloc.push_8
}

; ( len -- ptr )
fn salloc.alloc 1 1 {
	dup salloc.guard_size
	salloc.now swap
	alloc_buffer.len + pop alloc_buffer.len
}