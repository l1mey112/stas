auto label_c 1
auto main_fn_idx 1

fn label_allocate 0 1 {
	label_c dup ++ pop label_c
}

auto pos                          1
auto function_context             1
auto function_context_idx         1
auto is_still_evaluating_constant 1

; (inst data)
fn ir 2 0 {
	pos rot ir_stream<<
}

; (pos inst data)
fn ir_p 3 0 {
	ir_stream<<
}

const   sp.cap     { 1024                 }
const   sizeof(sp) { sizeof(u32) sp.cap * }
auto    sp.len     1
reserve sp         sizeof(sp)

fn sp<< 1 0 {
	sp.len sp.cap < assert
	sp sp.len sizeof(u32) * + swap w32
	sp.len ++ pop sp.len
}

; (count pos)
fn sp_push_p 2 0 {
	auto pos  1 pop pos
	auto retc 1 pop retc

	0
	while dup retc < {
		pos sp<<
		++
	}
	drop
}

; (count)
fn sp_push 1 0 {
	pos sp_push_p
}

; (argc retc)
fn sp_assert 2 0 {
	auto retc 1 pop retc
	auto _argc 1 pop _argc
	
	sp.len _argc < if {
		pos "not enough values to consume for operation" error.Tok
	}
	
	sp.len _argc - pop sp.len

	retc pos sp_push_p
}

; (string_view -- idx)
fn is_global_var_name 1 1 {
	global_var_context.len 0 = if {
		drop
		UINT64_MAX
		ret
	}

	auto lit 2
	string_view_to_str pop lit

	0
	while dup global_var_context.len < {
		dup global_var_context[] rVariable.name string_view_to_str
		lit streq if {
			ret
		}
		++
	}
	drop
	UINT64_MAX
}

; (string_view -- idx)
fn is_constant_name 1 1 {
	toplevel_constants.len 0 = if {
		drop
		UINT64_MAX
		ret
	}

	auto lit 2
	string_view_to_str pop lit

	0
	while dup toplevel_constants.len < {
		dup toplevel_constants[] rConstant.name string_view_to_str
		lit streq if {
			ret
		}
		++
	}
	drop
	UINT64_MAX
}

; (string_view -- idx)
fn is_function_name 1 1 {
	functions.len 0 = if {
		drop
		UINT64_MAX
		ret
	}

	auto lit 2
	string_view_to_str pop lit

	0
	while dup functions.len < {
		dup functions[] rFunction.name string_view_to_str
		lit streq if {
			ret
		}
		++
	}
	drop
	UINT64_MAX
}

; (string_view -- bool)
fn is_toplevel_name_bool 1 1 {
	dup is_constant_name   UINT64_MAX != swap
	    is_global_var_name UINT64_MAX != |
}

; (string_view -- idx)
fn search_variable_name 1 1 {
	var_context.len 0 = if {
		drop
		UINT64_MAX
		ret
	}

	auto lit 2
	string_view_to_str pop lit

	var_context.len 1 -
	while 1 {
		dup var_context[] rVariable.name string_view_to_str
		lit streq if {
			ret
		}

		dup 0 = if {
			break
		}
		--
	}
	drop
	UINT64_MAX
}

; (toplevel_constants[idx] token_stream[idx])
fn replace_as_constant 2 0 {
	auto const_idx 1
	swap pop const_idx
	
	dup token_stream[]
	    const_idx toplevel_constants[] rConstant.tok token_stream[]
	    swap
		sizeof(Token)
	    memcpy

	dup token_stream[] Token.tok  Tok.number_lit                                 w32
	    token_stream[] Token.data const_idx toplevel_constants[] rConstant.value w64
}

fn parse.push_empty_scope 0 0 {
	; (typ sp sp2 idx label_id label_id2 var_scope)
		ScopeTyp.scope
		sp.len
		0
		pos
		0
		0
		var_context.len
	scope_context<<
}

fn parse.fn_decl 0 0 {
	scope_context.len 0 > if {
		scope_context.top Scope.idx r32
		'unclosed scope for constant expression'
		error.Tok
	}
	auto fn_c 1 pos pop fn_c
	pos 4 + pop pos

	pos token_stream.len >= if {
		fn_c 'unexpected EOF when parsing function' error.Tok
	}
	fn_c 1 + token_stream[] rToken.tok Tok.name != if {
		fn_c 1 + 'function name must not be an intrinsic' error.Tok
	}
	fn_c 2 + token_stream[] rToken.tok Tok.number_lit !=
	fn_c 3 + token_stream[] rToken.tok Tok.number_lit !=
	| if {
		fn_c 'function return and argument counts must be numbers' error.Tok
	}
	fn_c 4 + token_stream[] rToken.tok Tok.l_cb != if {
		fn_c 'new scope must proceed after function header' error.Tok
	}

	auto sv 1
		fn_c 1 + token_stream[] rToken.data
	pop sv

	sv is_function_name UINT64_MAX != if {
		fn_c 1 + 'duplicate function name' error.Tok
	}
	sv is_toplevel_name_bool if {
		fn_c 1 + 'function name already exists as variable name' error.Tok
	}

	auto _argc 1 fn_c 2 + token_stream[] rToken.data pop _argc
	auto  retc 1 fn_c 3 + token_stream[] rToken.data pop  retc
	
	; functions[] body but without assertion
	functions.len sizeof(Function) * functions + pop function_context
	functions.len                                pop function_context_idx
		_argc
		retc
		sv
		ir_stream.len
		0 ; end_inst
		0 ; a_sp
		sv string_view_to_str 'main' streq if {
			_argc 0 !=
			retc  0 != | if {
				fn_c 'the main function must accept and return zero values' error.Tok
			}
			functions.len pop main_fn_idx

			1 1  ; is_used, forbid_inline
		} else {
			0 0  ; is_used, forbid_inline
		}
	functions<<

	fn_c Inst.fn_prelude function_context_idx ir_p
	
	sp.len          0 = assert
	var_context.len 0 = assert

	_argc fn_c sp_push_p
}

; ( -- VarTyp string_view size)
fn parse.any_variable 0 3 {
	auto rs_c 1
	pos pop rs_c

	pos 2 + pop pos
	pos token_stream.len >= if {
		rs_c 'unexpected EOF when parsing variable decl' error.Tok
	}

	rs_c token_stream[] rToken.tok
	Tok.reserve = if {
		VarTyp.buffer
	} else {
		VarTyp.stack
	}

	; (VarTyp)
	auto sv 1

	rs_c 1 + token_stream[] dup
	rToken.tok Tok.name != if {
		rs_c 1 + 'variable name must not be an intrinsic' error.Tok
	}
	rToken.data pop sv
	sv is_function_name UINT64_MAX != if {
		rs_c 1 + 'variable decl must not be a function' error.Tok
	}

	pos token_stream[] rToken.tok
	Tok.name = if {
		auto c_name 1
		pos token_stream[] rToken.data is_constant_name pop c_name

		c_name UINT64_MAX != if {
			c_name pos replace_as_constant
		}
	}
	pos token_stream[] rToken.tok
	Tok.number_lit != if {
		pos 'variable decl must specify size in bytes' error.Tok
	}

	sv
	
	rs_c token_stream[] rToken.tok
	Tok.reserve = if {
		pos token_stream[] rToken.data
	} else {
		pos token_stream[] rToken.data
		8 *
	}
	; (VarTyp string_view size)
}

fn parse.fn_complex_body_tokens 0 0 {
	pos token_stream[] rToken.tok

	; fn ir   (inst data)
	; fn ir_p (pos inst data)

	dup dup Tok.reserve = swap Tok.auto = | if {
		auto search_p 1
		0 pop search_p

	;	scope_context.len 0 != if {
	;		scope_context.top rScope.var_scope
	;		pop search_p
	;		
	;		scope_context.len 1 -
	;		while 1 {
	;			dup scope_context[] rScope.typ
	;			ScopeTyp.while_block = if {
	;				pos 'cannot define variables inside of while headers' error.Tok
	;			}
	;	
	;			dup 0 = if {
	;				break
	;			}
	;			--
	;		}
	;		drop
	;	}
		; TODO: Maybe above should be removed? I don't see a problem with it.
		;       Make sure to keep `search_p` though.
		
		parse.any_variable
		; (VarTyp string_view size)
		auto var_string 2
		over string_view_to_str pop var_string

		var_context.len search_p != if {
			var_context.len 1 -
			while 1 {
				dup var_context[] rVariable.name
				string_view_to_str var_string
				streq if {
					pos 1 - 'duplicate variable name' error.Tok
				}

				dup search_p = if {
					break
				}
				--
			}
			drop
		}

		auto size 1
		dup pop size

		; (VarTyp string_view size)

		; (typ size a_sp idx name)
					                        swap
			function_context rFunction.a_sp swap
			pos 2 -                         swap
		var_context<<

		function_context Function.a_sp
			function_context rFunction.a_sp size +
		w32
		function_context Function.forbid_inline 1 w8
	} elif dup Tok.pop = {
		pos ++ pop pos
		pos token_stream.len >= if {
			pos 1 - 'unexpected EOF when parsing pop' error.Tok
		}
		pos token_stream[] rToken.tok Tok.name != if {
			pos 'expected name after pop' error.Tok
		}

		auto sv    1 pos token_stream[] rToken.data pop sv
		auto f_idx 1 UINT64_MAX                     pop f_idx
		auto found 1 false                          pop found

		sv search_variable_name pop f_idx
		f_idx UINT64_MAX != if {
			auto var 1
			f_idx var_context[] pop var

			-> 0 {
				var rVariable.typ VarTyp.buffer = if {
					pos 'cannot pop into a buffer' error.Tok
				} elif var rVariable.typ VarTyp.stack = {
					auto to_pop 1
					var rVariable.size 8 / pop to_pop

					sp.len to_pop < if {
						pos 'not enought values on the stack to pop into an automatic variable' error.Tok
					}

					sp.len to_pop - pop sp.len

					Inst.pop_local_stack_var
						to_pop 32 <<
						var rVariable.a_sp |
					ir
				} else {
					0 assert
				}
			}
			true pop found
		}
		found ! if {
			sv is_global_var_name pop f_idx
			f_idx UINT64_MAX != if {
				auto var 1
				f_idx global_var_context[] pop var

				-> 0 {
					var rVariable.typ VarTyp.buffer = if {
						pos 'cannot pop into a buffer' error.Tok
					} elif var rVariable.typ VarTyp.stack = {
						auto to_pop 1
						var rVariable.size 8 / pop to_pop

						sp.len to_pop < if {
							pos 'not enought values on the stack to pop into an automatic variable' error.Tok
						}
						sp.len to_pop - pop sp.len

						Inst.pop_global_stack_var f_idx ir
					} else {
						0 assert
					}
				}
				true pop found
			}
		}
		found ! if {
			pos 'unknown auto variable' error.Tok
		}
	} elif dup Tok.name = {
		auto sv    1 pos token_stream[] rToken.data pop sv
		auto f_idx 1 UINT64_MAX                     pop f_idx
		auto found 1 false                          pop found

		; functions cannot be shadowed but variables can

		sv is_function_name pop f_idx
		f_idx UINT64_MAX != if {
			Inst.fn_call f_idx ir

			sp.len
			f_idx functions[] rFunction.argc
			< if {
				pos 'not enough values to consume for function call' error.Tok
			}

			f_idx functions[] rFunction.argc
			f_idx functions[] rFunction.retc
			sp_assert

			true pop found
		}
		found ! if {
			sv search_variable_name pop f_idx
			f_idx UINT64_MAX != if {
				auto var 1
				f_idx var_context[] pop var

				-> 0 {
					var rVariable.typ VarTyp.buffer = if {
						Inst.push_local_addr var rVariable.a_sp ir
						1 sp_push
					} elif var rVariable.typ VarTyp.stack = {
						auto to_push 1
						var rVariable.size 8 / pop to_push

						Inst.push_local_stack_var
							to_push 32 <<
							var rVariable.a_sp |
						ir

						to_push sp_push
					} else {
						0 assert
					}
				}
				true pop found
			}
		}
		found ! if {
			sv is_global_var_name pop f_idx
			f_idx UINT64_MAX != if {
				auto var 1
				f_idx global_var_context[] pop var

				-> 0 {
					var rVariable.typ VarTyp.buffer = if {
						Inst.push_global_var_name f_idx ir
						1 sp_push
					} elif var rVariable.typ VarTyp.stack = {
						Inst.push_global_stack_var f_idx ir
						var rVariable.size 8 / sp_push
					} else {
						0 assert
					}
				}
				true pop found
			}
		}
		found ! if {
			sv is_constant_name pop f_idx
			f_idx UINT64_MAX != if {
				auto c_name 1
				sv is_constant_name pop c_name

				c_name UINT64_MAX != if {
					c_name pos replace_as_constant
					pos -- pop pos
					true pop found
				}
			}
		}
		found ! if {
			pos 'unknown function call or variable' error.Tok
		}
	} elif dup Tok.if_block = {
		sp.len 0 = if {
			pos 'no value on stack to consume for if statement' error.Tok
		}
		sp.len -- pop sp.len

		auto lbl 1
		label_allocate pop lbl

		; (typ sp sp2 idx label_id label_id2 var_scope)
			ScopeTyp.if_block
			sp.len
			0
			pos
			lbl
			label_allocate
			var_context.len
		scope_context<<

		Inst.do_cond_jmp lbl ir
		
		pos ++ pop pos
		pos token_stream.len >= if {
			pos 1 - 'unexpected EOF when parsing if statement' error.Tok
		}
		pos token_stream[] rToken.tok Tok.l_cb != if {
			pos 'a scope must come after an if statement' error.Tok
		}
	} elif dup Tok.while_block = {
		auto lbl 1
		label_allocate pop lbl

		Inst.label lbl ir

		; (typ sp sp2 idx label_id label_id2 var_scope)
			ScopeTyp.while_block
			sp.len
			0
			pos
			lbl
			0
			var_context.len
		scope_context<<
	} elif dup Tok.break_block = {
		scope_context.len 0 = if {
			pos 'not inside while loop body' error.Tok
		}

		auto f 1
		false pop f

		scope_context.len 1 -
		while 1 {
			auto scope 1
			dup scope_context[] pop scope

			scope rScope.typ ScopeTyp.while_block_scope = if {
				Inst.do_jmp scope rScope.label_id ir
				
				true pop f
				break
			}

			dup 0 = if {
				break
			}
			--
		}
		drop

		f ! if {
			pos 'not inside while loop body' error.Tok
		}
	} elif dup Tok.continue_block = {
		scope_context.len 0 = if {
			pos 'not inside while loop body' error.Tok
		}

		auto f 1
		false pop f

		scope_context.len 1 -
		while 1 {
			dup scope_context[] rScope.typ
			ScopeTyp.while_block_scope = if {
				dup --
				scope_context[] dup rScope.typ ScopeTyp.while_block = assert
				
				Inst.do_jmp swap rScope.label_id ir
				true pop f
				break
			}

			dup 0 = if {
				break
			}
			--
		}
		drop

		f ! if {
			pos 'not inside while loop body' error.Tok
		}
	} elif dup Tok.l_cb = {
		scope_context.len 0 != if {
			scope_context.top rScope.typ
			dup ScopeTyp.while_block = if {
				sp.len 0 = if {
					pos 'no value on stack to consume for while header' error.Tok
				}
				sp.len -- pop sp.len
				auto lbl 1
				label_allocate pop lbl

				; (typ sp sp2 idx label_id label_id2 var_scope)
					ScopeTyp.while_block_scope
					sp.len
					0
					pos
					lbl
					0
					var_context.len
				scope_context<<

				Inst.do_cond_jmp lbl ir
			} elif dup ScopeTyp.elif_block = {
				reserve scope sizeof(Scope)
				scope scope_context.pop_into_ptr

				sp.len 0 = if {
					pos 'no value on stack to consume for else if statement' error.Tok
				}
				sp.len -- pop sp.len
				auto lbl 1
				label_allocate pop lbl

				; (typ sp sp2 idx label_id label_id2 var_scope)
					ScopeTyp.elif_block_scope
					scope rScope.sp
					scope rScope.sp2
					pos
					lbl
					scope rScope.label_id2
					var_context.len
				scope_context<<

				Inst.do_cond_jmp lbl ir
			} else {
				parse.push_empty_scope
			}
			drop
		} else {
			parse.push_empty_scope
		}
	} elif dup Tok.r_cb = {
		scope_context.len 0 != if {
			reserve scope sizeof(Scope)
			scope scope_context.pop_into_ptr

			scope rScope.var_scope pop var_context.len

			scope rScope.typ
			dup ScopeTyp.while_block_scope = if {
				reserve while_header sizeof(Scope)
				while_header scope_context.pop_into_ptr

				Inst.do_jmp while_header rScope.label_id ir
				Inst.label  scope        rScope.label_id ir
			} elif dup ScopeTyp.scope = {
				; nop
			} elif dup ScopeTyp.checked_scope = {
				sp.len scope rScope.sp > if {
					scope rScope.idx
						'scope assertation failed, more values on the stack than expected (TODO: write count)'
					error.Tok
				} elif sp.len scope rScope.sp < {
					scope rScope.idx
						'scope assertation failed, less values on the stack than expected (TODO: write count)'
					error.Tok
				}
			} elif dup ScopeTyp.if_block = {
				pos 1 + token_stream.len <
				pos 1 + token_stream[] rToken.tok Tok.else_block =
				& if {
					pos ++ pop pos

					auto lbl 1
					label_allocate pop lbl

					; (typ sp sp2 idx label_id label_id2 var_scope)
						ScopeTyp.else_block_scope
						sp.len
						sp.len
						pos
						lbl
						UINT32_MAX
						var_context.len
					scope_context<<

					scope rScope.sp pop sp.len
					Inst.do_jmp lbl ir

					pos ++ pop pos
					pos token_stream.len >= if {
						pos 1 - 'unexpected EOF when parsing else statement' error.Tok
					}
					pos token_stream[] rToken.tok Tok.l_cb != if {
						pos 'a scope must come after an else statement' error.Tok
					}
					Inst.label scope rScope.label_id ir
				} elif
					pos 1 + token_stream.len <
					pos 1 + token_stream[] rToken.tok Tok.elif_block =
				& {
					pos ++ pop pos

					; (typ sp sp2 idx label_id label_id2 var_scope)
						ScopeTyp.elif_block
						scope rScope.sp
						sp.len
						pos
						label_allocate
						scope rScope.label_id2
						var_context.len
					scope_context<<

					scope rScope.sp pop sp.len
					Inst.do_jmp scope rScope.label_id ir
					Inst.label scope rScope.label_id2 ir
				} else {
					scope rScope.sp sp.len != if {
						pos 'the stack must remain the same with single branches' error.Tok
					}
					Inst.label scope rScope.label_id ir
				}
			} elif dup ScopeTyp.elif_block_scope = {
				pos 1 + token_stream.len <
					pos 1 + token_stream[] rToken.tok
					dup  Tok.elif_block =
					swap Tok.else_block =
				| &
				if {
					pos ++ pop pos

					; (typ sp sp2 idx label_id label_id2 var_scope)
						pos token_stream[] rToken.tok
						dup Tok.elif_block = if {
							ScopeTyp.elif_block
						} elif dup Tok.else_block = {
							ScopeTyp.else_block_scope
						} else {
							0 0 assert -> 'unreachable'
						}
						swap drop

						scope rScope.sp
						scope rScope.sp2
						pos
						label_allocate
						scope rScope.label_id2
						var_context.len
					scope_context<<

					scope rScope.sp2 sp.len != if {
						pos 'the stack must remain the same each with else if branch' error.Tok
					}
					scope rScope.sp pop sp.len

					Inst.do_jmp scope rScope.label_id2 ir
					Inst.label  scope rScope.label_id  ir

					pos token_stream[] rToken.tok Tok.else_block = if {
						pos ++ pop pos
						pos token_stream.len >= if {
							pos 1 - 'unexpected EOF when parsing else statement' error.Tok
						}
						pos token_stream[] rToken.tok Tok.l_cb != if {
							pos 'a scope must come after an else statement' error.Tok
						}
					}
				} else {
					scope rScope.sp2 sp.len != if {
						pos 'the stack must remain the same with single branches' error.Tok
					}
					Inst.label scope rScope.label_id  ir
					Inst.label scope rScope.label_id2 ir
				}
			} elif dup ScopeTyp.else_block_scope = {
				sp.len scope rScope.sp2 > if {
					scope rScope.idx
						'unbalanced stack on both if and else branches, else has more than expected (TODO: write count)'
					error.Tok
				} elif sp.len scope rScope.sp2 < {
					scope rScope.idx
						'unbalanced stack on both if and else branches, else has less than expected (TODO: write count)'
					error.Tok
				}
				Inst.label scope rScope.label_id ir
				scope rScope.label_id2 UINT32_MAX != if {
					Inst.label scope rScope.label_id2 ir
				}
			} else {
				0 assert -> 'unreachable'
			}
			drop
		} else {
			function_context Function.end_inst ir_stream.len w32
			Inst.fn_leave function_context_idx ir

			-> 0 {
				sp.len function_context rFunction.retc > if {
					function_context rFunction.start_inst.idx
					'unhandled data on the stack' error.Tok
				} elif sp.len function_context rFunction.retc < {
					function_context rFunction.start_inst.idx
					'not enough values on the stack on function return' error.Tok
				}
			}

			0          pop sp.len
			0          pop var_context.len
			NULL       pop function_context
			UINT64_MAX pop function_context_idx
		}
	} elif dup Tok.ret = {
		Inst.fn_leave function_context_idx ir

		-> 0 {
			sp.len function_context rFunction.retc > if {
				function_context rFunction.start_inst.idx
				'unhandled data on the stack' error.Tok
			} elif sp.len function_context rFunction.retc < {
				function_context rFunction.start_inst.idx
				'not enough values on the stack on function return' error.Tok
			}
		}

		scope_context.len 0 != if {
			scope_context.top
			rScope.typ ScopeTyp.if_block = if {
				scope_context.top rScope.sp pop sp.len
			}
		}

		function_context Function.forbid_inline 1 w8
	} elif dup Tok.arrw = {
		auto arrw_c 1
		pos pop arrw_c
		
		pos 2 + pop pos
		pos token_stream.len >= if {
			arrw_c 'unexpected EOF when parsing checked scope' error.Tok
		}

		arrw_c 1 + token_stream[]
		dup rToken.tok Tok.number_lit != if {
			arrw_c 1 + 'a number must appear after the arrow assert operator' error.Tok
		}

		auto num 1
		rToken.data pop num

		; (typ sp sp2 idx label_id label_id2 var_scope)
			ScopeTyp.checked_scope
			num sp.len +
			0
			arrw_c
			0
			0
			var_context.len
		scope_context<<

		pos token_stream[]
		rToken.tok Tok.l_cb != if {
			pos 'a scope must come after the arrow assert operator' error.Tok
		}
	} elif dup Tok.string_lit = {
		Inst.push_str pos token_stream[] rToken.data ir
		2 sp_push
	} elif dup Tok.w8 = {
		Inst.w8 0 ir
		2 0 sp_assert
	} elif dup Tok.w16 = {
		Inst.w16 0 ir
		2 0 sp_assert
	} elif dup Tok.w32 = {
		Inst.w32 0 ir
		2 0 sp_assert
	} elif dup Tok.w64 = {
		Inst.w64 0 ir
		2 0 sp_assert
	} elif dup Tok.r8 = {
		Inst.r8 0 ir
		1 1 sp_assert
	} elif dup Tok.r16 = {
		Inst.r16 0 ir
		1 1 sp_assert
	} elif dup Tok.r32 = {
		Inst.r32 0 ir
		1 1 sp_assert
	} elif dup Tok.r64 = {
		Inst.r64 0 ir
		1 1 sp_assert
	} elif dup Tok.trap_breakpoint = {
		Inst.trap_breakpoint 0 ir
		1 1 sp_assert
	} elif dup Tok.syscall0 = {
		Inst.syscall0 0 ir
		1 1 sp_assert
	} elif dup Tok.syscall1 = {
		Inst.syscall0 0 ir
		2 1 sp_assert
	} elif dup Tok.syscall2 = {
		Inst.syscall0 0 ir
		3 1 sp_assert
	} elif dup Tok.syscall3 = {
		Inst.syscall0 0 ir
		4 1 sp_assert
	} elif dup Tok.syscall4 = {
		Inst.syscall0 0 ir
		5 1 sp_assert
	} elif dup Tok.syscall5 = {
		Inst.syscall0 0 ir
		6 1 sp_assert
	} elif dup Tok.syscall6 = {
		Inst.syscall0 0 ir
		7 1 sp_assert
	} elif dup Tok.argc = {
		Inst.push_argc 0 ir
		1 sp_push
	} elif dup Tok.argv = {
		Inst.push_argv 0 ir
		1 sp_push
	} else {
		pos 'unknown function local token' error.Tok
	}
	
	drop
}

fn parse.assert 0 0 {
	sp.len 0 = if {
		pos 'no value on stack to consume for assertion' error.Tok
	}
	sp.len -- pop sp.len

	; Inst._assert

	auto contains_message 1
	false pop contains_message

	pos 2 + token_stream.len < if {
		pos 1 + token_stream[] rToken.tok Tok.arrw       = 
		pos 2 + token_stream[] rToken.tok Tok.string_lit =
		& if {
			true pop contains_message
		}
	}

	auto sv 1			
	pos '' error.Tok.to_string_view pop sv

	contains_message if {	
		sv "assertation failed, '" push_string_view
		sv
			pos 2 + token_stream[] rToken.data string_view_to_str
		push_string_view
		sv "'\n" push_string_view
		sv push_nul

		Inst._assert sv ir
		pos 2 + pop pos
	} else {
		sv 'assertation failed\n' push_string_view
		sv push_nul
		
		Inst._assert sv ir
	}
}

fn parse.fn_body_tokens 0 0 {
	pos token_stream[] rToken.tok

	; fn ir   (inst data)
	; fn ir_p (pos inst data)

	dup Tok._assert = if {
		parse.assert
	} elif dup Tok.number_lit = {
		Inst.push_num pos token_stream[] rToken.data ir
		1 sp_push
	} elif dup Tok.plus = {
		Inst.plus 0 ir
		2 1 sp_assert
	} elif dup Tok.sub = {
		Inst.sub 0 ir
		2 1 sp_assert
	} elif dup Tok.mul = {
		Inst.mul 0 ir
		2 1 sp_assert
	} elif dup Tok.div = {
		Inst.div 0 ir
		2 1 sp_assert
	} elif dup Tok.mod = {
		Inst.mod 0 ir
		2 1 sp_assert
	} elif dup Tok.inc = {
		Inst.inc 0 ir
		1 1 sp_assert
	} elif dup Tok.dec = {
		Inst.dec 0 ir
		1 1 sp_assert
	} elif dup Tok.divmod = {
		Inst.divmod 0 ir
		2 2 sp_assert
	} elif dup Tok.shr = {
		Inst.shr 0 ir
		2 1 sp_assert
	} elif dup Tok.shl = {
		Inst.shl 0 ir
		2 1 sp_assert
	} elif dup Tok.b_and = {
		Inst.b_and 0 ir
		2 1 sp_assert
	} elif dup Tok.b_or = {
		Inst.b_or 0 ir
		2 1 sp_assert
	} elif dup Tok.b_not = {
		Inst.b_not 0 ir
		1 1 sp_assert
	} elif dup Tok.not = {
		Inst.not 0 ir
		1 1 sp_assert
	} elif dup Tok.b_xor = {
		Inst.b_xor 0 ir
		2 1 sp_assert
	} elif dup Tok.swap = {
		Inst.swap 0 ir
		2 2 sp_assert
	} elif dup Tok.dup = {
		Inst.dup 0 ir
		1 2 sp_assert
	} elif dup Tok.over = {
		Inst.over 0 ir
		2 3 sp_assert
	} elif dup Tok.over2 = {
		Inst.over2 0 ir
		3 4 sp_assert
	} elif dup Tok.rot = {
		Inst.rot 0 ir
		3 3 sp_assert
	} elif dup Tok.rot4 = {
		Inst.rot4 0 ir
		4 4 sp_assert
	} elif dup Tok.drop = {
		Inst.drop 0 ir
		1 0 sp_assert
	} elif dup Tok.equ = {
		Inst.equ 0 ir
		2 1 sp_assert
	} elif dup Tok.nequ = {
		Inst.nequ 0 ir
		2 1 sp_assert
	} elif dup Tok.gt = {
		Inst.gt 0 ir
		2 1 sp_assert
	} elif dup Tok.lt = {
		Inst.lt 0 ir
		2 1 sp_assert
	} elif dup Tok.gte = {
		Inst.gte 0 ir
		2 1 sp_assert
	} elif dup Tok.lte = {
		Inst.lte 0 ir
		2 1 sp_assert
	} elif dup Tok.s_gt = {
		Inst.s_gt 0 ir
		2 1 sp_assert
	} elif dup Tok.s_lt = {
		Inst.s_lt 0 ir
		2 1 sp_assert
	} elif dup Tok.s_gte = {
		Inst.s_gte 0 ir
		2 1 sp_assert
	} elif dup Tok.s_lte = {
		Inst.s_lte 0 ir
		2 1 sp_assert
	} else {
		function_context NULL != if {
			parse.fn_complex_body_tokens
		} else {
			pos token_stream[] rToken.tok Tok.const_expression = if {
				pos 'cannot define constant expression inside a constant expression' error.Tok
			}
			pos 'unknown constant expression token' error.Tok
		}
	}
	drop
}

fn parse.const_expression 0 0 {
	auto rs_c 1 pos pop rs_c

	pos 2 + pop pos
	pos token_stream.len >= if {
		rs_c 'unexpected EOF when parsing constant expression' error.Tok
	}

	auto sv 1
	rs_c 1 + token_stream[] rToken.tok Tok.name != if {
		rs_c 1 + 'constant expression name must not be in instrinsic' error.Tok
	}
	rs_c 1 + token_stream[] rToken.data pop sv

	sv is_function_name UINT64_MAX != if {
		rs_c 1 + 'duplicate constant expression name as existing function name' error.Tok
	}
	sv is_toplevel_name_bool if {
		rs_c 1 + 'constant expression name as duplicate top level variable name' error.Tok
	}
	pos token_stream[] rToken.tok Tok.l_cb != if {
		pos 'a scope must follow a const expression decl' error.Tok
	}

	; (name inst_start tok value)
		sv
		ir_stream.len
		pos 1 -
		UINT64_MAX
	toplevel_constants<<

	true pop is_still_evaluating_constant
	sp.len 0 = assert
}

fn stas.parse 0 0 {
	UINT64_MAX pop main_fn_idx
	0 pop pos

	while pos token_stream.len < {
		is_still_evaluating_constant true != 
		function_context NULL =
		& if {
			pos token_stream[] rToken.tok

			dup Tok.const_expression = if {
				parse.const_expression
			} elif dup Tok.fn_decl = {
				parse.fn_decl
			} elif dup dup Tok.reserve = swap Tok.auto = | {
				parse.any_variable
				over is_toplevel_name_bool if {
					pos 1 - 'duplicate top level variable name' error.Tok
				}
				; (VarTyp string_view size)

				; (typ size a_sp idx name)
					        swap
					0       swap
					pos 2 - swap
				global_var_context<<
			} else {
				pos "unknown toplevel token" error.Tok
			}
			drop
		} else {
			function_context NULL = if {
				pos token_stream[] rToken.tok

				dup Tok.r_cb = if {
					0 pop const_stack.len

					toplevel_constants.top rConstant.inst_start
					while dup ir_stream.len < {
						dup eval_one_inst assert -> 'unreachable'
						++
					}
					drop

					const_stack.len 0 = if {
						toplevel_constants.top rConstant.tok
						'constant expression must contain a value'
						error.Tok
					}

					const_stack.len 1 > if {
						toplevel_constants.top rConstant.tok
						'more than one unhandled value on left on the constant expression'
						error.Tok
					}

					0                                           pop sp.len
					toplevel_constants.top rConstant.inst_start pop ir_stream.len

					toplevel_constants.top Constant.value
						const_stack.pop
					w64

					false pop is_still_evaluating_constant
					const_stack.len 0 = assert
				} elif dup Tok.name = {
					auto c_name 1
					pos token_stream[] rToken.data is_constant_name pop c_name

					c_name UINT64_MAX != if {
						c_name pos replace_as_constant
						pos -- pop pos
					} else {
						pos 'unknown constant variable' error.Tok
					}
				} else {
					parse.fn_body_tokens
				}
				drop
			} else {
				parse.fn_body_tokens
			}
		}

		pos ++ pop pos
	}

	var_context.len 0 = assert 
	sp.len          0 = assert

	scope_context.len 0 != if {
		token_stream.len 1 - "unclosed scopes on parser finish" error.Tok
	}
	function_context NULL != if {
		token_stream.len 1 - "unexpected EOF when parsing function, scopes may be unclosed" error.Tok	
	}
}