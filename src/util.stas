fn error.start_bold_error 0 0 {
	27 eputc '[1m'  eputs
	27 eputc '[31m' eputs
}

fn error.end_bold_error 0 0 {
	27 eputc '[39m' eputs
	27 eputc '[22m' eputs
}

; (str len)
fn error.generic_fatal_noexit 2 0 {
	error.start_bold_error
	"FATAL: " eputs
	error.end_bold_error
	eputs
	10 eputc
}

; (str len)
fn error.generic_fatal 2 0 {
	error.generic_fatal_noexit
	1 exit
}

; (row col file str len)
fn error.Loc 5 0 {
	auto msg 2 pop msg

	error.start_bold_error
		string_view_to_str puts ':'  puts
		swap
		1 + putu                ':'  puts
		1 + putu                ': ' puts
	error.end_bold_error
	
	msg eputs endl
	1 exit
}

; (idx str len)
fn error.Tok 3 0 {
	rot rot
	
	error.start_bold_error
		token_stream[] Token.location_print
	error.end_bold_error
	
	eputs endl
	1 exit
}

; (idx str len -- string_view)
fn error.Tok.to_string_view 3 1 {
	reserve num_buf 21
 
	auto sv 1 new_empty_string pop sv
	rot rot
	
	sv 27 push_char sv '[1m'  push_string_view
	sv 27 push_char sv '[31m' push_string_view
		token_stream[]
		dup Token.file r64
		    string_view_to_str sv rot push_string_view
		    sv ':'  push_string_view
		dup Token.row r32 1 + num_buf swap itoa sv rot push_string_view
		    sv ':'  push_string_view
		    Token.col r32 1 + num_buf swap itoa sv rot push_string_view
		    sv ': ' push_string_view
	sv 27 push_char sv '[39m' push_string_view
	sv 27 push_char sv '[22m' push_string_view
	
	sv rot   push_string_view

	sv push_nul
	sv
}