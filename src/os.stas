const OS.linux   1
const OS.freebsd 2

; ( -- OS )
fn OS.get_host 0 1 {
	comptime ?def TARGET_LINUX if {
		OS.linux
		ret
	}
	comptime ?def TARGET_FREEBSD if {
		OS.freebsd
		ret
	}
	0
	0 assert -> 'unknown host OS'
}

; ( str len -- OS bool )
fn OS.from_str? 2 2 {
	auto str 2 pop str

	str "linux" streq if {
		OS.linux true
	} elif str "freebsd" streq {
		OS.freebsd true
	} else {
		0 false
	}
}

; ( OS -- str len )
fn OS.to_pretty_str 1 2 {
	auto os 1 pop os
	
	os OS.linux = if {
		"Linux"
	} elif os OS.freebsd = {
		"FreeBSD"
	} else {
		NULL 0
		0 assert -> 'unreachable'
	}
}

; ( OS -- ELFOSABI bool )
fn OS.elf_os_abi_from? 1 2 {
	auto os 1 pop os

	os OS.linux = if {
		ELFOSABI_SYSV true
	} elif os OS.freebsd = {
		ELFOSABI_FREEBSD true
	} else {
		0 false
	}
}